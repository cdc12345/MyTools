plugins {
    id 'java'
}

group = 'org.cdc.mytools'
version = '1.0'

configurations {
    implementation.extendsFrom export
}

repositories {
    mavenCentral()
    maven { url "https://repo.gradle.org/gradle/libs-releases" } // for gradle-tooling-api
    flatDir { dirs mcreator_path + '/lib' }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation project(':Fountain')
    implementation project(':MCreator')
    implementation 'com.github.javaparser:javaparser-core:3.27.1'

    project(':MCreator').afterEvaluate(() ->
            project(":MCreator").configurations.named("implementation").get().dependencies.each {
                //noinspection ForeignDelegate
                implementation it
            }
    )
}

tasks.jar {
    dependsOn("runDataGen")

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveFileName.set("devutils.zip")

    from {
        configurations.export.collect { it.isDirectory() ? it : zipTree(it) }
    }

    doLast {
        copy {
            from "build/libs/devutils.zip"
            into mcreator_plugins_path
        }
    }
}

test {
    useJUnitPlatform()
}

tasks.processResources{
    exclude "lang/*.modifier"
}

tasks.register("runDataGen",JavaExec).configure {
    group = "build"
    main("org.cdc.framework.FountainBaseMain")
    classpath = sourceSets.main.runtimeClasspath
    workingDir(projectDir)

    doLast{
        copy {
            from sourceSets.main.resources
            into project.layout.buildDirectory.file("resources/main")
            exclude "lang/*.modifier"
        }
    }
}